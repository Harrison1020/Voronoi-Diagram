SUBROUTINE TESTPF(N,NL,NB,INDEXP,X,Y,NP,LID,LPF,LES,LEE,LELS,LERS,&
     NPFD,LPFD,IFLAG)

  ! TEST EDGES IN THE PFD LIST. CALLS ANGLE, AUGMEN, DELETE, SAME, UPDATE

  PARAMETER (LLEN=300)
  LOGICAL SAME
  INTEGER J1
  REAL*8 X,Y,TOP,BOTTOM,TOP1,BOTT1,TOP2,BOTT2
  DIMENSION INDEXP(-1:LLEN),X(0:LLEN),Y(0:LLEN),NP(-LLEN:LLEN),LID(7*LLEN),&
       LPF(7*LLEN),LES(3*LLEN),LEE(3*LLEN),LELS(3*LLEN),LERS(3*LLEN),&
       LPFD(3*LLEN),IFLAG(LLEN)
  J1=0
  J=0
1 J=J+1
  IF(J.GT.NPFD) RETURN
  K=LPFD(J)
  IF(K.EQ.0) GOTO 1

  ! SET LATER DUPLICATE EDGE K TO ZERO
  IF(J.NE.NPFD) THEN
     DO L=J+1,NPFD
        IF(K.EQ.LPFD(L)) LPFD(L)=0
     END DO
  END IF

  ! GET NEXT EDGE IF K IS ON BOUNDARY OR DIVIDES CONCAVE QUADRILATERAL
  IF(LELS(K).EQ.0.OR.LERS(K).EQ.0.OR.SAME(X,Y,LES(K),LELS(K),LERS(K),X(LEE(K)),&
       Y(LEE(K)))) GOTO 1

  ! APPLY MIN-MAX ANGLE TEST TO K
  CALL ANGLE(X,Y,LES(K),LELS(K),LERS(K),TOP1,BOTT1)
  CALL ANGLE(X,Y,LEE(K),LELS(K),LERS(K),TOP,BOTTOM)
  IF(TOP1*BOTTOM.LT.TOP*BOTT1) THEN
     TOP1=TOP
     BOTT1=BOTTOM
  END IF
  CALL ANGLE(X,Y,LES(K),LEE(K),LELS(K),TOP2,BOTT2)
  CALL ANGLE(X,Y,LES(K),LEE(K),LERS(K),TOP,BOTTOM)
  IF(TOP2*BOTTOM.LT.TOP*BOTT2) THEN
     TOP2=TOP
     BOTT2=BOTTOM
  END IF
  IF(TOP1*BOTT2.GE.TOP2*BOTT1) GOTO 1

  ! REDEFINE EDGE K TO JOIN LELS(K) AND LERS(K)
  L=LELS(K)
  LELS(K)=LEE(K)
  LEE(K)=LERS(K)
  LERS(K)=LES(K)
  LES(K)=L

  ! ZERO FALGS FOR AFFECTED POINTS
  IFLAG(LES(K))=0
  IFLAG(LEE(K))=0
  IFLAG(LELS(K))=0
  IFLAG(LERS(K))=0

  ! AUGMENT SPOKE LISTS FOR LES(K) AND LEE(K)
  CALL AUGMEN(N,NL,NB,INDEXP,NP,LID,LPF,LES(K),K,J1)
  CALL AUGMEN(N,NL,NB,INDEXP,NP,LID,LPF,LEE(K),K,J1)

  ! DELETE EDGE K FROM SPOKE LILST
  CALL DELETE(LID,LPF,NP(LELS(K)),K)
  CALL DELETE(LID,LPF,NP(LERS(K)),K)

  ! PUT EDGE OF THE QUADRILATERAL INTO PFD LIST AND UPDATE SIDE POINTS
  IF(NPFD.NE.3*(LLEN-1)) THEN
     CALL UPDATE(LID,LPF,LES,LEE,LELS,LERS,IND,NP(LES(K)),LERS(K),&
          LELS(K),LEE(K))
     NPFD=NPFD+1
     LPFD(NPFD)=IND
     CALL UPDATE(LID,LPF,LES,LEE,LELS,LERS,IND,NP(LES(K)),LELS(K),&
          LERS(K),LEE(K))
     NPFD=NPFD+1
     LPFD(NPFD)=IND
     CALL UPDATE(LID,LPF,LES,LEE,LELS,LERS,IND,NP(LEE(K)),LELS(K),&
          LERS(K),LES(K))
     NPFD=NPFD+1
     LPFD(NPFD)=IND
     CALL UPDATE(LID,LPF,LES,LEE,LELS,LERS,IND,NP(LEE(K)),LERS(K),&
          LELS(K),LES(K))
     NPFD=NPFD+1
     LPFD(NPFD)=IND
     GOTO 1
  END IF

  ! FATAL ERROR MESSAGE
  WRITE(6,100)
  STOP
100 FORMAT('ARRAY OVERFLOW FOR PFD LSIT (TESTPF)')
END SUBROUTINE TESTPF
